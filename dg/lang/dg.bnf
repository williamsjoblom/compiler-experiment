unit = Stmt*						@CompilationUnit;

Stmt = Expr ";"						@ExprStmt
     | FunDecl
     | VarDecl ";"
     | Pln ";"
     | IfStmt
     | Block
     | ReturnStmt;

## Expression.

Expr = <num>						@Number
     | <id>						@Var
     || :group <id> ArgList				@FunCall
     |  :group "(" Expr ")"
     || :left "(" Expr ("," Expr)* ")"	 		@TupleExpr
     || :left Expr "." <num>				@TupleIndexAccess
     |	:left Expr "." <id>				@TupleNameAccess
     || :right "-" Expr					@NegExpr
     || :left Expr "*" Expr 				@MulExpr
     |  :left Expr "/" Expr    				@DivExpr
     || :left Expr "+" Expr				@AddExpr
     |  :left Expr "-" Expr				@SubExpr
     || :right Expr "=" Expr				@AssExpr
     |  :right Expr "==" Expr				@EqExpr
     |  :right Expr ">=" Expr                           @GeExpr
     |  :right Expr "<=" Expr                           @LeExpr
     |  :right Expr ">" Expr                            @GExpr
     |  :right Expr "<" Expr                            @LExpr;
     
ArgList =  "(" (Expr ("," Expr)*)? ")"			@ParamList;

Cond = Expr						@Cond;
IfStmt = "if" "(" Cond ")" Stmt ("else" Stmt)?		@IfStmt;


Block = "{" Stmt* "}"					@Block;

Type = "(" Type ("," Type)* ")"				@TupleType
     | "(" <id> ":" Type ("," <id> ":" Type)* ")"	@NamedTupleType
     | InnerType					@Type;
InnerType = <id>					@InnerType;

FunDecl = "fun" <id> ParamList
	  ("->" Type)? Block				@FunDecl;

ParamList = "(" (Param ("," Param)*)? ")" 		@ParamList;
Param = <id> ":" Type					@Param;

VarDecl = "var" <id> ":" Type? ("=" Expr)?		@VarDecl;

Pln = "pln" Expr					@Pln;
ReturnStmt = "return" Expr?				@ReturnStmt;
